apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'java'

repositories {
    jcenter()
    maven {
        // For: "org.pentaho:pentaho-aggdesigner-algorithm:5.1.3-jhyde"
        // required by: "gobblin-runtime:0.10.0 > org.apache.hive:hive-exec:1.0.1"
        url "http://repo.spring.io/plugins-release"
    }
}

configurations {
    gobblin.exclude group: 'com.google.guava'     // cannot be "compile", otherwise won't pass ":voldemort-bnp:validateDependencies"
    resolvedIncorrectly
}

dependencies {
    gobblin 'com.linkedin.gobblin:gobblin-runtime:0.10.0'
    gobblin 'com.linkedin.gobblin:gobblin-data-management:0.10.0'
    gobblin 'com.linkedin.gobblin:gobblin-throttling-service-client:0.10.0'
    gobblin 'com.linkedin.gobblin:gobblin-throttling-service-api-rest-client:0.10.0'
    gobblin 'com.linkedin.gobblin:gobblin-throttling-service-api-data-template:0.10.0'
    gobblin 'com.linkedin.gobblin:gobblin-metrics:0.10.0'
    resolvedIncorrectly 'com.google.guava:guava:15.0'
}

shadowJar {
    zip64 true
    configurations = [project.configurations.gobblin, project.configurations.resolvedIncorrectly]

    // Hadoop dependencies are expected to be provided by Azkaban.
    // If Azkaban is not included in your deployment, you may need to remove the following block
    dependencies {
        exclude(dependency('org.apache.hadoop:.*'))
    }

    def troublemakers = ['org.apache.avro', 'com.google.protobuf', 'com.google.common']
    troublemakers.each {
        relocate it, 'gobblin-dep/' + it
    }
    doLast {
        def file = new File("${buildDir}/libs/${project.name}-all.jar")
        println '================================'
        println "${project.name} jar size: ${((float)(file.length()/1024/1024)).round(2)} MB"
        println '================================'
    }

}

jar {
    dependsOn shadowJar
    doLast {
        print "${buildDir}"
        def gobblinJar = new File("${buildDir}/libs/${project.name}-all.jar")
        gobblinJar.renameTo(new File("${buildDir}/libs/${project.name}.jar"))
    }
}
